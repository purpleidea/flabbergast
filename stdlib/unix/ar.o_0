Introduction{{{Templates for generating UNIX \Mono{ar} archives, which are conveniently text-only.}}}
{{{A BSD \Mono{ar} style archive}}}
archive_tmpl : Template {
	{{{The collection of \{file_tmpl} files.}}}
	args : Required
	value : "!<arch>\n" & (For file : args Concat file.value With "")
	{{{The default user ID to create files.}}}
	uid : 0
	{{{The default group ID to create files.}}}
	gid : 0
	{{{The modification time, as a time or seconds since the epoch.}}}
	mtime : time_lib.now.utc
	{{{The user permission bits.}}}
	user : perm_lib.rw
	{{{The group permission bits.}}}
	group : perm_lib.r
	{{{The other permission bits.}}}
	other : perm_lib.r
}

{{{A file in a compressed archive}}}
file_tmpl : Template {
	{{{The file name.}}}
	file_name : Required
	{{{The file contents.}}}
	contents : Required
	uid : Used
	gid : Used
	mtime : Used
	user : Used
	group : Used
	other : Used

	file_name_length : If utils_lib.str_find(file_name, str : "/") Is Null Then utils_lib.str_utf8_length(file_name) Else Error "ar archives do not support directories."
	# If the file name is more than 16 characters, use the BSD `ar` encoding.
	file_header : If file_name_length > 16 Then "#1/" & file_name_length Else file_name
	header_suffix : If file_name_length > 16 Then file_name Else ""
	packed_length : utils_lib.str_utf8_length(contents, header_suffix)
	header_perms :
		utils_lib.str_pad(uid Enforce Int, length : 6) &
		utils_lib.str_pad(gid Enforce Int, length : 6) &
		utils_lib.str_pad((perm_lib.permission_from_mode {}).octal, length : 8)
	value :
		utils_lib.str_pad(file_header, length : 16) &
		utils_lib.str_pad(If mtime Is Int Then mtime Else mtime.epoch To Int, length : 12) &
		header_perms &
		utils_lib.str_pad(packed_length, length : 10) &
		"`\n" & header_suffix & contents & (If packed_length % 2 == 1 Then "\n" Else "")
}

{{{A symbolic link in a compressed archive. These are not supported.}}}
symlink_tmpl : Template {
	{{{The file name.}}}
	file_name : Required
	{{{The target of the link.}}}
	target : Required
	value : Error "Symbolic links are not supported."
	uid : Used
	gid : Used
	mtime : Used
	user : Used
	group : Used
	other : Used
}

time_lib : From lib:time
utils_lib : From lib:utils
perm_lib : From lib:unix/perm
